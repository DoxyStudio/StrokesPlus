; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "StrokesPlus"
#define MyAppSigned "_Signed"
#define MyAppPlatform "x86"

#define BaseFolder "C:\Users\Rob\Documents\Development\Visual Studio 2008\Projects\StrokesPlus"

#define ExeName BaseFolder+"\bin\Release\"+MyAppPlatform+"\StrokesPlus.exe"

#define MyAppVersion GetFileVersion(ExeName)

#define MyAppPublisher "Rob Larkin"
#define MyAppURL "http://www.strokesplus.com/"
#define MyAppExeName "StrokesPlus.exe"

[Code]
procedure MyBeforeInstall();
begin
  MsgBox('In order to use this signed version of StrokesPlus on Windows Vista/7/8 with UAC enabled, you must install StrokesPlus under the Program Files directory or the certificate will be ignored and S+ will not be able to interact with certain Windows 8 screens or programs running as Admin.', mbInformation, MB_OK);
end;

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)

AppId={{6CE25EDE-B75E-452A-829D-587591C84362}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
#if MyAppPlatform == "x64"
  DefaultDirName={pf64}\{#MyAppName}
#else
  DefaultDirName={pf32}\{#MyAppName}
#endif
#if MyAppSigned == ""
;  DefaultDirName={userappdata}\{#MyAppName}
  OutputBaseFilename=StrokesPlusSetup_{#MyAppVersion}_{#MyAppPlatform}
#else
  OutputBaseFilename=StrokesPlusSetup_{#MyAppVersion}_{#MyAppPlatform}{#MyAppSigned}
#endif
DefaultGroupName={#MyAppName}
AllowNoIcons=yes
LicenseFile={#BaseFolder}\bin\StrokesPlus_{#MyAppPlatform}{#MyAppSigned}\Licenses.txt
OutputDir={#BaseFolder}\bin\Upload\
SetupIconFile={#BaseFolder}\StrokesPlus\StrokesPlus.ico
Compression=lzma
SolidCompression=yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Files]
#if MyAppSigned == ""
  Source: "{#BaseFolder}\bin\StrokesPlus_{#MyAppPlatform}{#MyAppSigned}\StrokesPlus.exe"; DestDir: "{app}"; Flags: ignoreversion
#else
  Source: "{#BaseFolder}\bin\StrokesPlus_{#MyAppPlatform}{#MyAppSigned}\StrokesPlus.exe"; DestDir: "{app}"; Flags: ignoreversion; BeforeInstall: MyBeforeInstall;
#endif
Source: "{#BaseFolder}\bin\StrokesPlus_{#MyAppPlatform}{#MyAppSigned}\ActionHelp.xml"; DestDir: "{userappdata}\{#MyAppName}"; Flags: ignoreversion onlyifdoesntexist
Source: "{#BaseFolder}\bin\StrokesPlus_{#MyAppPlatform}{#MyAppSigned}\Licenses.txt"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#BaseFolder}\bin\StrokesPlus_{#MyAppPlatform}{#MyAppSigned}\StrokesPlus.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#BaseFolder}\bin\StrokesPlus_{#MyAppPlatform}{#MyAppSigned}\StrokesPlus.html"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#BaseFolder}\bin\StrokesPlus_{#MyAppPlatform}{#MyAppSigned}\StrokesPlus.lua"; DestDir: "{userappdata}\{#MyAppName}"; Flags: ignoreversion onlyifdoesntexist
Source: "{#BaseFolder}\bin\StrokesPlus_{#MyAppPlatform}{#MyAppSigned}\StrokesPlus.xml"; DestDir: "{userappdata}\{#MyAppName}"; Flags: ignoreversion onlyifdoesntexist
Source: "{#BaseFolder}\bin\StrokesPlus_{#MyAppPlatform}{#MyAppSigned}\images\*"; DestDir: "{app}\images"; Flags: ignoreversion 
Source: "{#BaseFolder}\bin\StrokesPlus.cer"; DestDir: "{app}"; Flags: ignoreversion 
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{group}\{cm:ProgramOnTheWeb,{#MyAppName}}"; Filename: "{#MyAppURL}"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"
;#if MyAppSigned == ""
  Name: "{userstartup}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
;#endif

[Run]
#if MyAppSigned == "_Signed"
  Filename: "cmd.exe"; Parameters: "/c certutil.exe -f -addstore Root ""{app}\StrokesPlus.cer"""; StatusMsg: "Installing Certificate..."; Flags: runascurrentuser
#endif

